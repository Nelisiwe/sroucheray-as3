<languageVersion : 1.0;>

kernel OverlayBlendMode
<   namespace : "org.sroucheray";
    vendor : "Stéphane Roucheray";
    version : 1;
    description : "Photoshop Overlay BlendMode"; >
{
    parameter float percent
    <
        minValue:       0.0;
        maxValue:       1.0;
        defaultValue:   1.0;
    >;
    
    input image4 frontImage;
    input image4 backImage;
    output pixel4 dst;
   
   void evaluatePixel()
   {
        float2 coord = outCoord();
        pixel4 cs = sampleNearest(frontImage, coord);
        pixel4 cb = sampleNearest(backImage, coord);
        pixel4 fullBlend = cs;
        
        //Red
        if (cb.r <= 0.5){
            fullBlend.r = 2.0 * cb.r * cs.r;
        }else{
            fullBlend.r = cs.r + (2.0 * cb.r - 1.0) - (2.0 * cb.r - 1.0) * cs.r;
        }
        //Green
        if (cb.g <= 0.5){
            fullBlend.g = 2.0 * cb.g * cs.g;
        }else{
            fullBlend.g = cs.g + (2.0 * cb.g - 1.0) - (2.0 * cb.g - 1.0) * cs.g;
        }
        //Blue
        if (cb.b <= 0.5){
            fullBlend.b = 2.0 * cb.b * cs.b;
        }else{
            fullBlend.b = cs.b + (2.0 * cb.b - 1.0) - (2.0 * cb.b - 1.0) * cs.b;
        }
        
        dst = mix(cs, fullBlend, percent);
   }
}
